

	;********************************
	;*           NOTIFY             *
	;*   Original ACORN version     *
	;********************************

	;* Assembled using beebasm 1.09 *

	LOADADDR =? &0E23
	MASTER =?TRUE

	GSINIT=?&FFC2
	GSREAD=?&FFC5
	OSARGS=?&FFDA
	OSASCI=?&FFE3
	OSNEWL=?&FFE7
	OSWRCH=?&FFEE
	OSWORD=?&FFF1
	OSBYTE=?&FFF4

	TXTPTR=?&F2

	CLEAR LOADADDR, LOADADDR+&200
	ORG LOADADDR

.NOTIFY_START
{
.L_0E23
	JMP L_0E38

.D_0E26
	EQUS " __",0

.D_0E2A
	EQUS "__ ", 7, "000 : "

.D_0E34
	EQUB 0, 0, 0, 24

	;THIS CODE WILL BE OVERWRITEN LATER
.OWBLK

.L_0E38
	LDA #&01
	LDY #&00

.L_0E3C
	LDX #&A8
	JSR OSARGS
	LDA &A8
	STA TXTPTR
	LDA &A9
	STA TXTPTR+1

IF MASTER
	CLC
	JSR GSINIT
	BEQ L_0EC6_SYNTAX
ELSE
	LDA #&00
.L_0E4B
	JSR OSARGS
	EOR #&05
	STA &AA
	BNE L_0E66

	LDA #&02
	JSR OSARGS
	CMP #&02
	BNE L_0E66

	CLC
	JSR GSINIT

.L_0E61
	JSR GSREAD
	BCC L_0E61

.L_0E66
	CLC
	JSR GSINIT
	BEQ L_0EC6_SYNTAX

	PHA
	LDA &AA
	BEQ L_0E7A

	TYA
	PHA
	LDY #&05
	JSR S_0FB3
	PLA
	TAY

.L_0E7A
	PLA
ENDIF

	CMP #'.'
	BEQ L_0E87

	CMP #'0'
	BCC L_0EBF

	CMP #'9'+1
	BCS L_0EBF


.L_0E87
	JSR S_0E8F
	BCS L_0EAA

	JMP L_0F6A

.S_0E8F
	LDA #&00
	JSR S_0E96
	BCC L_0EC5

.S_0E96
	TAX
	LDA #&00
	STA &AC

.L_0E9B
	JSR GSREAD
	BCS L_0EC2

	CMP #'.'
	BEQ L_0EC3

	CMP #'0'
	BCC L_0F0D_BADNUM

	CMP #'9'+1

.L_0EAA
	BCS L_0F0D_BADNUM

	AND #&0F
	STA &AD
	ASL &AC
	LDA &AC
	ASL A
	ASL A
	ADC &AC
	ADC &AD
	STA &AC
	JMP L_0E9B

.L_0EBF
	JMP L_0F1D

.L_0EC2
	CLC

.L_0EC3
	LDA &AC

.L_0EC5
	RTS

.L_0EC6_SYNTAX
	BRK
	EQUS &DE, "Syntax : *Notify <station number>|<User name> <Message>", 0

IF NOT(MASTER) AND LOADADDR = &0E23
	ORG &0F09
	EQUD &FFFF0E23;Execution address
ENDIF

IF MASTER AND LOADADDR <> &0E23
.J_0F0D_BADNUM
ENDIF

.L_0F0D_BADNUM
IF NOT(MASTER)
.J_0F0D_BADNUM
	JSR S_0FAC
ENDIF
	BRK
	EQUS &F0, "Bad number", 0

IF MASTER AND LOADADDR = &0E23
	ORG &0F09
	EQUD &FFFF0E23;Execution address

.J_0F0D_BADNUM
	JMP L_0F0D_BADNUM
ENDIF

.L_0F1D
	;READ USER NAME
	CLC
	JSR GSINIT
	LDX #&07

.L_0F23
	JSR GSREAD
	STA OWBLK+2,X
	INX
	BCC L_0F23

	LDA #&0D
	STA OWBLK+1,X;TERMINATOR
	
	TYA
	PHA
	LDY #&03

.L_0F35
	LDA D_0E34,Y
	STA OWBLK+2,Y
	DEY
	BPL L_0F35

	STX OWBLK+3;BLOCK SIZE
	JSR S_0FBA_SERVER;FUNCTION 24 
	LDA OWBLK+5;RC, SUCCESS?
	BEQ L_0F54;IF USER FOUND

IF NOT(MASTER)
	JSR S_0FAC
ENDIF

	;REPORT ERROR RETURNED BY FILESERVER, i.e. "Not logged on"
	LDA #0
	STA OWBLK+19
	JMP OWBLK+4

.L_0F54
	;USER LOGGED ON
	;OWBLK?6 = NET
	;OWBLK?7 = STATION
	LDA #&11
	STA OWBLK+5;CONTROL BYTE
	LDX #LO(OWBLK+5)
	LDY #HI(OWBLK+5)
	LDA #&13;LOCAL STATION? 17 TRANSLATE NUMBER
	JSR OSWORD
	
	PLA
	TAY
	LDA OWBLK+7;STATION
	LDX OWBLK+8;TRANSLATED NETWORK

.L_0F6A
	STA OWBLK+3;STATION
	CMP #&00
	BEQ J_0F0D_BADNUM

	CMP #&FF
	BEQ J_0F0D_BADNUM

	STX OWBLK+4;NET
	CPX #&FF
	BEQ J_0F0D_BADNUM

	;COPY MESSAGE
	LDX #&0D
	SEC
	JSR GSINIT

.L_0F82
	JSR GSREAD
	STA OWBLK+2,X
	INX
	BCC L_0F82

	TXA
	ADC #&07
	TAX;BLOCK SIZE = HEADER SIZE + MESSAGE LENGTH
	
	LDY #&09

.L_0F91
	LDA D_0E26,Y
	STA OWBLK+2,X
	LDA D_0E2A,Y
	STA OWBLK+5,Y
	DEX
	DEY
	BPL L_0F91

	LDA #1
	STA OWBLK+2;SEND TEST STRING
	
	JSR S_0FC3

IF NOT(MASTER)
	JSR S_0FBA_SERVER

.S_0FAC
	LDA &AA
	BEQ L_0FFB
	EOR #&05
	TAY

.S_0FB3
	LDA #&8F
	LDX #&12
	JMP OSBYTE
ENDIF

.S_0FBA_SERVER
	LDX #LO(OWBLK+2)
	LDY #HI(OWBLK+2)
	LDA #&14;COMMUNICATE WITH FILESERVER
	JMP OSWORD

.S_0FC3
	LDA #&08
	STA OWBLK
	LDA #&13
	LDX #LO(OWBLK)
	LDY #HI(OWBLK)
	JSR OSWORD;LOCAL STATION? 8 READ STATION NUMBER
	
	LDA OWBLK+1;LOCAL STATION NUMBER
	
	LDX #&07
	STX &AC
	
	TAY
	
	LDA #100
	JSR S_0FE5
	LDA #10
	JSR S_0FE5
	LDA #1

.S_0FE5
	STA &B8
	TYA
	LDX #&2F
	SEC

.L_0FEB
	INX
	SBC &B8
	BCS L_0FEB
	
	ADC &B8
	TAY
	TXA
	
	LDX &AC
	INC &AC
	STA OWBLK+2,X

.L_0FFB
	RTS
}
.NOTIFY_END

	SAVE "$.NOTIFY", NOTIFY_START, NOTIFY_END

