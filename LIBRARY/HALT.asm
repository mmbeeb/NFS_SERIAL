

	;********************************
	;*            HALT              *
	;*           UNHALT             *
	;*    By Martin Mather 2022     *
	;********************************

	;* Assembled using beebasm 1.09 *
	
	;BASED ON *VIEW
	
	LOADADDR=?&0E23

	GSINIT=?&FFC2
	GSREAD=?&FFC5
	OSARGS=?&FFDA
	OSASCI=?&FFE3
	OSNEWL=?&FFE7
	OSWRCH=?&FFEE
	OSWORD=?&FFF1
	OSBYTE=?&FFF4

	TXTPTR=?&F2

MACRO HALTUNHALT immop
{
	USERID_SIZE=10;MAXIMUM SIZE OF FILE SERVER USER ID

	;STATION NUMBER?
	JSR S_DEB7
	BCC L_DD3C;IF READ, A=STATION, X=NET

	;USER NAME?
	LDX #&100-USERID_SIZE-2

.L_DD09
	INX
	BNE L_DD14;BUFFER NOT FULL

	JMP L_DE3F_SYNTAX

.L_DD14
	LDA (TXTPTR),Y
	CMP #' '+1
	BCS L_DD18;IF A > ' '

	LDA #&0D

.L_DD18
	STA FS_BLK+7-&100+USERID_SIZE+1,X
	INY
	EOR #&0D
	BNE L_DD09
	
	;GET STATION & NET FROM FILE SERVER
	LDX #LO(FS_BLK)
	LDY #HI(FS_BLK)
	LDA #&14
	JSR OSWORD

	LDA FS_BLK+3
	BEQ L_DD36;USER FOUND

	;REPORT ERROR RETURNED BY FILE SERVER, I.E. "Not logged on"
	LDA #0
	STA FS_BLK+17;TERMINATE MESSAGE
	JMP FS_BLK+2

.L_DD36
	LDA FS_BLK+5;STATION
	LDX FS_BLK+6;NET

	;GOT STATION & NET
.L_DD3C
	STA OW_BLK+2
	STX OW_BLK+3	
	
	;TRANSMIT & POLL
.TRANSMIT
	DEC L_DE3F_SYNTAX;USED AS COUNTER
	BEQ L_DE30_NOTLISTENING;GIVE UP?

	LDX #LO(OW_BLK)
	LDY #HI(OW_BLK)
	LDA #&10
	JSR OSWORD;TRANSMIT

	;POLLING LOOP (WITH ESCAPE)
.L_DE1F
	BIT &FF
	BPL L_DE6B;NO ESCAPE!

	LDA #&7E
	JSR OSBYTE
	
	BRK
	EQUS &11, "Escape"

.L_DE30_NOTLISTENING
	BRK
	EQUS &A2, "Not listening"

.L_DE3F_SYNTAX
	BRK
	EQUS &DE, "Syntax : *"
IF immop=&86
	EQUS "Halt"
ELSE
	EQUS "Unhalt"
ENDIF
	EQUS " <Station>|<User>", 0

	;POLL TRANSMIT
.L_DE6B
	LDA #&32
	JSR OSBYTE
	TXA
	BMI L_DE1F
	BNE TRANSMIT;ERROR, TRY AGAIN?

	RTS
	
IF LOADADDR=&0E23
	ORG &0F09
	EQUD &FFFF0E23;Execution address
ENDIF

	;HALT/CONTINUE
.OW_BLK
	EQUB immop
	EQUW 0


	;OSWORD &14 BLOCK
	;GET USER'S STATION FROM FILE SERVER
.FS_BLK
	EQUB 0, 30, 0;, 24
	;NOTE THE FOLLOWING BYTE IS 'CLC', I.E. DECIMAL 24 (FUNCTION NO.)

	;ATTEMPT TO READ STATION & NET NUMBERS
	;EXITS WITH C=0 IF READ, A=STATION, X=NET
.S_DEB7
{
	CLC
	LDY #&FF

.L_DEB8
	INY
	LDA (TXTPTR),Y
	EOR #&0D
	BEQ J_DE3F_SYNTAX

	EOR #'-'
	BCS L_DEC6
	BNE L_DEB8

	SEC

.L_DEC6
	BEQ L_DEB8
	
	EOR #&20
	CMP #'A'
	BCS L_DEFD

	LDA #0
	JSR S_DED6
	BCC L_DEFD

	INY

.S_DED6
{
	TAX;X=NET
	LDA #0
	STA &B2

.L_DEDB
	LDA (TXTPTR),Y
	CMP #'@'
	BCS L_DEFA;>=64

	CMP #'.'
	BEQ L_DEFB
	BMI L_DEFA

	AND #&0F
	STA &B3
	ASL &B2
	LDA &B2
	ASL A
	ASL A
	ADC &B2
	ADC &B3
	STA &B2
	INY
	BNE L_DEDB

.L_DEFA
	CLC

.L_DEFB
	LDA &B2
}

.L_DEFD
	RTS
	
.J_DE3F_SYNTAX
	JMP L_DE3F_SYNTAX
}
}
ENDMACRO


	CLEAR LOADADDR, LOADADDR+&200
	ORG LOADADDR

.HALT_START
{
	HALTUNHALT &86
}
.HALT_END

IF HI(HALT_START)=&DD;MASTER
	SAVE "M\$.HALT", HALT_START, HALT_END
ELIF HI(HALT_START)=&0E
	SAVE "E\$.HALT", HALT_START, HALT_END
ELSE
	SAVE "O\$.HALT", HALT_START, HALT_END
ENDIF
	
	
	CLEAR LOADADDR, LOADADDR+&200
	ORG LOADADDR

.UNHALT_START
{
	HALTUNHALT &87
}
.UNHALT_END

IF HI(UNHALT_START)=&DD;MASTER
	SAVE "M\$.UNHALT", UNHALT_START, UNHALT_END
ELIF HI(UNHALT_START)=&0E
	SAVE "E\$.UNHALT", UNHALT_START, UNHALT_END
ELSE
	SAVE "O\$.UNHALT", UNHALT_START, UNHALT_END
ENDIF
	
	