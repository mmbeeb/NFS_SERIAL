

	;********************************
	;*          FINDLIB             *
	;*   Original ACORN version     *
	;********************************

	;* Assembled using beebasm 1.09 *

	;THIS IS THE MASTER VERSION.

	LOADADDR =?&0E23

	OSASCI=?&FFE3
	OSNEWL=?&FFE7
	OSWRCH=?&FFEE
	OSWORD=?&FFF1
	OSBYTE=?&FFF4
	OSFILE=?&FFDD
	OSFIND=?&FFCE

	TXTPTR=?&F2

	CLEAR LOADADDR, LOADADDR+&200
	ORG LOADADDR

.FINDLIB_START
{
.D_DD00
IF LOADADDR=&DD00
	LDX #&11
	LDA #&A1
	JSR OSBYTE
	TYA
	BPL L_DD17
	
	LDX #&00
	
.L_DD0C
	LDA D_DD45,X
	BEQ L_DD17
	
	JSR OSASCI
	INX
	BNE L_DD0C
ENDIF
	
.L_DD17

	;SAVE &A8-&AF
	LDX #&07
	
.L_DD19
	LDA &A8,X
	PHA
	DEX
	BPL L_DD19
	
	LDA #&01
	LDX #&A8
	LDY #&00
	JSR OSARGS
	
	LDA &A8
	STA TXTPTR
	LDA &A9
	STA TXTPTR+1
	
	CLC
	LDX #&00
	JSR GSINIT
	BNE L_DD6D
	
	;Default to "$.Library1"
.L_DD38
	LDA D_DE76,X
	STA D_DD00,X
	INX
	CMP #&0D
	BNE L_DD38
	BEQ L_DD81
	
.D_DD45
	EQUS "Find Library. 1.03", 13, 0

.L_DD59_TOOLONG
	BRK
	EQUS &28, "Argument too long", 0
	
.L_DD6D
	JSR GSREAD
	BEQ L_DD7C
	
	STA D_DD00,X
	CPX #&3C
	BCS L_DD59_TOOLONG
	
	INX
	BCC L_DD6D
	
.L_DD7C
	LDA #&0D
	STA D_DD00,X
	
.L_DD81
	LDA #&00
	STA &AA
	
.L_DD85
	LDA #&00
	STA OW_BLK
	LDA #&0A
	STA OW_BLK+1
	LDA #14
	STA OW_BLK+3;FUNCTION 14 = READ DISC NAMES
	LDA &AA
	INC &AA
	STA OW_BLK+7
	LDA #&01
	STA OW_BLK+8
	
	LDX #LO(OW_BLK)
	LDY #HI(OW_BLK)
	LDA #&14
	JSR OSWORD
	
	LDX #&00
	LDA OW_BLK+4
	BNE L_DDBC
	
	BRK
	EQUS &D6, "Not found", 0

	;BUILD PATH NAME
	
	;DRIVE NAME
.L_DDBC
	LDY #&10
	LDA OW_BLK+6,X
	STA D_DE76,X
	CMP #&20
	BEQ L_DDCC
	
	INX
	DEY
	BNE L_DDBC

	;SEPARATOR
.L_DDCC
	LDA #&2E
	STA D_DE76,X

	;DIR NAME
	LDY #&00
	
.L_DDD3
	LDA D_DD00,Y
	INX
	INY
	STA D_DE76,X
	CMP #&0D
	BNE L_DDD3
	
	LDA #&05;READ CAT INFO
	LDX #LO(FILE_BLK)
	LDY #HI(FILE_BLK)
	JSR OSFILE
	
	CMP #&02
	BEQ L_DE11;DIR FOUND
	
	CMP #&00
	BEQ L_DD85;NEXT DISC
	
IF LOADADDR=&0E23
	BNE skip;ALWAYS
	
	ORG &0F09
	
	EQUD &FFFF0E23
.skip
ENDIF

	;REPORT NOT FOUND
	LDX #&00
	STX D_DE75-2	
	LDA #&BE
	STA D_DE75-1
	
.L_DDFA
	INX
	LDA D_DE75-1,X
	CMP #&0D
	BNE L_DDFA
	
	LDY #&00
	
.L_DE04
	INX
	INY
	LDA D_DE3E-1,Y
	STA D_DE75-2,X
	BNE L_DE04
	
	JMP D_DE75-2
	
.L_DE11
	LDX #LO(D_DE70)
	LDY #HI(D_DE70)
	LDA #&40;OPEN FILE FOR INPUT ONLY
	JSR OSFIND;A=HANDLE
	PHA
	
	LDA #&06;READ CONTEXT HANDLES
	JSR S_DE52
	LDY D_DE60+3;Y=LIB HANDLE
	
	PLA
IF CPU=1
	PHY
ENDIF
	STA D_DE60+3;LIB HANDLE=A
IF CPU=0
	TYA
	PHA
ENDIF
	LDA #&07;WRITE CONTEXT HANDLES
	JSR S_DE52
IF CPU=1
	PLY
ELSE
	PLA
	TAY
ENDIF
	
	LDA #&00
	JSR OSFIND;CLOSE #Y
	
	;RESTORE &A8-&AF
	LDX #&00
	
.L_DE35
	PLA
	STA &A8,X
	INX
	CPX #&08
	BNE L_DE35
	
	RTS
	
.D_DE3E
	EQUS " is not a directory", 0
	
.S_DE52
	STA D_DE60;CONTROL BYTE
	LDX #LO(D_DE60)
	LDY #HI(D_DE60)
	LDA #&13
	JMP OSWORD

.FILE_BLK
	EQUW D_DE70;ADDRESS OF FILENAME
	
.D_DE60
	SKIP 16
	
.D_DE70
	EQUS "-NET-"
.D_DE75
	EQUS ":"
.D_DE76
.OW_BLK
	EQUS "$.Library1", 13

}
.FINDLIB_END

IF HI(FINDLIB_START)=&DD;MASTER
	SAVE "M\$.FINDLIB", FINDLIB_START, FINDLIB_END
ELIF HI(FINDLIB_START)=&0E
	SAVE "E\$.FINDLIB", FINDLIB_START, FINDLIB_END
ELSE
	SAVE "O\$.FINDLIB", FINDLIB_START, FINDLIB_END
ENDIF

